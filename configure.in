Dnl Process this file with autoconf to produce a configure script.
AC_INIT(Makefile.am)
AM_CONFIG_HEADER(config.h)

#
# Making releases:
#   *_MICRO_VERSION += 1;
#   *_INTERFACE_AGE += 1;
#   *_BINARY_AGE += 1;
# if any functions have been added, set *_INTERFACE_AGE to 0.
# if backwards compatibility has been broken,
# set *_BINARY_AGE and *_INTERFACE_AGE to 0.
#

# OpenWBEM
OPENWBEM_MAJOR_VERSION=2
OPENWBEM_MINOR_VERSION=9
OPENWBEM_MICRO_VERSION=1
OPENWBEM_INTERFACE_AGE=0
OPENWBEM_BINARY_AGE=0

# MOF
MOF_MAJOR_VERSION=2
MOF_MINOR_VERSION=9
MOF_MICRO_VERSION=1
MOF_INTERFACE_AGE=0
MOF_BINARY_AGE=0


# OpenWBEM versioning
OPENWBEM_VERSION=$OPENWBEM_MAJOR_VERSION.$OPENWBEM_MINOR_VERSION.$OPENWBEM_MICRO_VERSION
AC_SUBST(OPENWBEM_MAJOR_VERSION)
AC_SUBST(OPENWBEM_MINOR_VERSION)
AC_SUBST(OPENWBEM_MICRO_VERSION)
AC_SUBST(OPENWBEM_INTERFACE_AGE)
AC_SUBST(OPENWBEM_BINARY_AGE)
AC_SUBST(OPENWBEM_VERSION)

# OpenWBEM libtool versioning
OPENWBEM_RELEASE=$OPENWBEM_MAJOR_VERSION.$OPENWBEM_MINOR_VERSION
OPENWBEM_CURRENT=`expr $OPENWBEM_MICRO_VERSION - $OPENWBEM_INTERFACE_AGE`
OPENWBEM_REVISION=$OPENWBEM_INTERFACE_AGE
OPENWBEM_AGE=`expr $OPENWBEM_BINARY_AGE - $OPENWBEM_INTERFACE_AGE`
AC_SUBST(OPENWBEM_RELEASE)
AC_SUBST(OPENWBEM_CURRENT)
AC_SUBST(OPENWBEM_REVISION)
AC_SUBST(OPENWBEM_AGE)


# MOF versioning
MOF_VERSION=$MOF_MAJOR_VERSION.$MOF_MINOR_VERSION.$MOF_MICRO_VERSION
AC_SUBST(MOF_MAJOR_VERSION)
AC_SUBST(MOF_MINOR_VERSION)
AC_SUBST(MOF_MICRO_VERSION)
AC_SUBST(MOF_INTERFACE_AGE)
AC_SUBST(MOF_BINARY_AGE)
AC_SUBST(MOF_VERSION)

# MOF libtool versioning
MOF_RELEASE=$MOF_MAJOR_VERSION.$MOF_MINOR_VERSION
MOF_CURRENT=`expr $MOF_MICRO_VERSION - $MOF_INTERFACE_AGE`
MOF_REVISION=$MOF_INTERFACE_AGE
MOF_AGE=`expr $MOF_BINARY_AGE - $MOF_INTERFACE_AGE`
AC_SUBST(MOF_RELEASE)
AC_SUBST(MOF_CURRENT)
AC_SUBST(MOF_REVISION)
AC_SUBST(MOF_AGE)
                     
AM_INIT_AUTOMAKE(openwbem, $OPENWBEM_VERSION)
#AM_DISABLE_STATIC

dnl Checks for programs.
AC_CANONICAL_HOST
AC_PROG_CC
AC_PROG_CXX
AC_LANG_C
#AC_LANG_CPLUSPLUS
AM_PROG_LEX
AC_PROG_RANLIB
AM_CONDITIONAL(HAVE_FLEX, test x$LEX = xflex)
AC_PROG_YACC

AC_PATH_PROG(AR, ar, ar)
SAVED_AR=$AR
AR="\$(shell pwd)/\$(top_srcdir)/ow_ar.sh $SAVED_AR --"

AC_PATH_PROG(MV, mv, mv)
AC_PATH_PROG(RM, rm, rm)
AC_PATH_PROG(SED, sed, sed)

dnl BMMU check for perl
if test "$with_perl" != no; then
  #PGAC_CHECK_PERL_CONFIGS([archlibexp,privlibexp,useshrplib])
  PGAC_CHECK_PERL_CONFIG(archlibexp)
  PGAC_CHECK_PERL_CONFIG(privlibexp)
  PGAC_CHECK_PERL_CONFIG(useshrplib)
  PGAC_CHECK_PERL_EMBED_CCFLAGS
  PGAC_CHECK_PERL_EMBED_LDFLAGS
  saved_LDFLAGS="$LDFLAGS"
  saved_CFLAGS="$CFLAGS"
  CFLAGS="$CFLAGS $perl_embed_ccflags"
  LDFLAGS="$LDFLAGS $perl_embed_ldflags"

  # Basic test to check for compilation success
  AC_TRY_COMPILE(
	 [
#include <EXTERN.h>
#include <perl.h>
	 ],
	 [

	 ],
	 [
       AC_DEFINE(PERLIFC)
       HAVE_PERL=yes
       break;
	 ], 
	 [
       HAVE_PERL=no
	 ]
  )
  CFLAGS="$saved_CFLAGS"
  LDFLAGS="$saved_LDFLAGS"
fi
AM_CONDITIONAL(HAVE_PERL_EMBED, test x$HAVE_PERL = xyes)


dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADER(pthread.h, have_pthread=1)
AC_CHECK_HEADER(pth.h, have_pth=1)
use_pam=1
AC_CHECK_HEADERS(security/pam_misc.h security/pam_appl.h, 
	true, use_pam=0)

AM_CONDITIONAL(USE_PAM, test x$use_pam = x1)
if test x$use_pam = x1; then
    AC_DEFINE(HAVE_PAM)
else
	AC_MSG_WARN(*** No PAM headers found! The PAM authentication module ***)
	AC_MSG_WARN(*** will not be built. The config file defaults to      ***)
	AC_MSG_WARN(*** using PAM authentication.                           ***)
	AC_MSG_WARN(*** You must edit the config file and use a different   ***)
	AC_MSG_WARN(*** authentication module or else the daemon won't start***)
fi
AC_SUBST(platform_pamdir)


AC_ARG_ENABLE(zlib,
[  --disable-zlib     disable zlib integration])

if test "$enable_zlib" != no; then
  AC_CHECK_HEADERS(zlib.h, LIBS="$LIBS -lz")
fi

AC_CHECK_HEADERS(fcntl.h limits.h sys/file.h unistd.h byteswap.h pthread.h \
sys/cdefs.h sys/queue.h sys/int_types.h \
inttypes.h getopt.h)

# check for C++ headers
AC_LANG_CPLUSPLUS

AC_CHECK_HEADERS(streambuf streambuf.h ostream \
ostream.h istream istream.h hash_map ext/hash_map)

AC_LANG_C

AC_CHECK_FUNCS(getopt_long gethostbyname_r sched_yield backtrace)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_BIGENDIAN

AC_CHECK_SIZEOF(char, 1)
AC_CHECK_SIZEOF(short int, 2)
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(long int, 4)
AC_CHECK_SIZEOF(long long int, 8)
AC_CHECK_SIZEOF(float, 4)
AC_CHECK_SIZEOF(double, 8)
AC_CHECK_SIZEOF(long double, 8)

AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_CHECK_TYPE(u_int8_t, uint8_t)
AC_CHECK_TYPE(u_int16_t, uint16_t)
AC_CHECK_TYPE(u_int32_t, uint32_t)
AC_STRUCT_ST_BLKSIZE
AC_DECL_SYS_SIGLIST
TYPE_SOCKLEN_T

dnl Checks for library functions
AC_FUNC_MEMCMP
AC_FUNC_MMAP
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strerror strtoll strtoull)

#Figure out which platform dir to compile
case "$host" in
*-*-aix*)
	;;
*-*-cygwin*)
	;;
*-*-darwin*)
	PLATFORMDIR="posix"
	AC_DEFINE(DARWIN)
	PLAT_CFLAGS="-fno-common -D_REENTRANT"
	PLAT_CXXFLAGS="-fno-common -D_REENTRANT"
	CXXCOMMONLINKFLAGS=""
	CXXBINLINKFLAGS="-Wl,-Bdynamic"
	CXXSHAREDLIBLINKFLAGS="-dynamiclib -install_name \$@ -compatibility_version $OPENWBEM_VERSION -current_version $OPENWBEM_VERSION"
	PLAT_OPTFLAGS="-O3"
	PIDFILEDIR="/var/run"
	CXX="c++"
	;;
*-*-dgux*)
	;;
*-*-hpux10*)
	;;
*-*-hpux11*)
	;;
*-*-irix5*)
	;;
*-*-irix6*)
	;;
*-*-linux*)
	PLATFORMDIR="posix"
	AC_DEFINE(GNU_LINUX)
	PLAT_CFLAGS="-fPIC -D_REENTRANT -D_GNU_SOURCE -pipe"
	PLAT_CXXFLAGS="-fPIC -D_REENTRANT -D_GNU_SOURCE -pipe"
	CXXCOMMONLINKFLAGS=""
	CXXBINLINKFLAGS="-Wl,-Bdynamic $LDFLAGS"
	CXXSHAREDLIBLINKFLAGS='-shared -fPIC -Wl,-soname -Wl,$@'
	PLAT_OPTFLAGS="-O3"
	PIDFILEDIR="/var/run"
	;;
*-*-netbsd*)
	;;
*-*-freebsd*)
	;;
*-next-*)
	;;
*-*-solaris*)
	PLATFORMDIR="posix"
	AC_DEFINE(SOLARIS)
	LIBS="$LIBS -lsocket -lnsl"
	PLAT_CFLAGS="-fPIC -pthreads -D_REENTRANT -D__EXTENSIONS__ -D_POSIX_PTHREAD_SEMANTICS"
	PLAT_CXXFLAGS="-fPIC -pthreads -D_REENTRANT -D__EXTENSIONS__ -D_POSIX_PTHREAD_SEMANTICS"
	CXXCOMMONLINKFLAGS="-pthreads $LDFLAGS"
	CXXBINLINKFLAGS="-Wl,-Bdynamic"
	CXXSHAREDLIBLINKFLAGS='-shared -fPIC'
	PLAT_OPTFLAGS="-O3"
	PIDFILEDIR="/var/run"
	;;
*-*-sunof4*)
	;;
*-ncr-sysv*)
	;;
*-sni-svsv*)
	;;
*-*-sysv4.2*)
	;;
*-*-sysv5*)
	PLATFORMDIR="posix"
	AC_DEFINE(OPENUNIX)
	LIBS="$LIBS"
	PLAT_CFLAGS="-pthread -fPIC -D_REENTRANT"
	PLAT_CXXFLAGS="-pthread -fPIC -D_REENTRANT"
	CXXCOMMONLINKFLAGS="-pthread"
	#CXXBINLINKFLAGS="-Wl,-Bexport"
	CXXBINLINKFLAGS="-lsocket -Wl,-Bdynamic,-Bexport"
	#CXXSHAREDLIBLINKFLAGS='-fpic -shared -Wl,-h,$@,-z,text -lstdc++'
	#CXXSHAREDLIBLINKFLAGS='-shared -Wl,-h,$@,-z,text -nodefaultlibs'
	#CXXSHAREDLIBLINKFLAGS='-shared -G -h $@ -nodefaultlibs'
	CXXSHAREDLIBLINKFLAGS='-fPIC -shared -Wl,-h,$@,-z,text -nodefaultlibs'
	LDFLAGS="$LDFLAGS"
	PLAT_OPTFLAGS="-O3"
	PIDFILEDIR="/etc"
	;;
*-*-sysv*)
	;;
*-*-sco3.2v4*)
	;;
*-*-sco3.2v5*)
	PLATFORMDIR="posix"
	AC_DEFINE(OPENSERVER)
        AC_DEFINE(USE_GNU_PTH)
	LIBS="$LIBS -lpth -lsocket -lstdc++"
	PLAT_CFLAGS="-fPIC"
	PLAT_CXXFLAGS="-fPIC"
	CXXCOMMONLINKFLAGS=""
	CXXBINLINKFLAGS="-Wl,-Bdynamic,-Bexport"
	CXXSHAREDLIBLINKFLAGS='-fPIC -shared -G -h $@'
	LDFLAGS="$LDFLAGS"
	CXXLINK="g++"
	PLAT_OPTFLAGS="-O0"
	PIDFILEDIR="/etc"
	;;
*-dec-osf*)
	;;
esac

# Set up the linker
if test -z "$CXXLINK" ; then
  CXXLINK="$CXX"
fi
if test -z "$CLINK" ; then
  CLINK="$CC"
fi

AC_DEFINE_UNQUOTED(PIDFILE_DIR, "${PIDFILEDIR}")

AC_SUBST(PLATFORMDIR)

AC_ARG_ENABLE(debug-mode,
[  --enable-debug-mode     enable debugging mode],
  [
	AC_DEFINE(DEBUG)
	CFLAGS="$PLAT_CFLAGS -g -Werror -Wall -W"
	CXXFLAGS="$PLAT_CXXFLAGS -g -Werror -Wall -W -Woverloaded-virtual"
	#CXXFLAGS="$PLAT_CXXFLAGS -g -Werror -Wall -W -Woverloaded-virtual -fno-implicit-templates -fno-implicit-inline-templates -fno-implement-inlines"
  ],[
    CFLAGS="$PLAT_CFLAGS -g -DNDEBUG $PLAT_OPTFLAGS"
    CXXFLAGS="$PLAT_CXXFLAGS -g -DNDEBUG $PLAT_OPTFLAGS"
    #CXXFLAGS="$PLAT_CXXFLAGS -DNDEBUG $PLAT_OPTFLAGS -fno-implicit-templates -fno-implicit-inline-templates -fno-implement-inlines"
  ])

AC_ARG_ENABLE(openslp,
[  --disable-openslp     disable openslp integration])

if test "$enable_openslp" != no; then
	AC_CHECK_HEADERS(slp.h, LIBS="$LIBS -lslp"; OW_HAVE_SLP=1)
	AM_CONDITIONAL(HAVE_SLP, test "x$OW_HAVE_SLP" = "x1")
fi

AC_ARG_ENABLE(acls,
[  --disable-acls     disable acls])

if test "$enable_acls" = no; then
	AC_DEFINE(DISABLE_ACLS)
fi

AC_ARG_ENABLE(profile-mode,
[  --enable-profile-mode     enable profiling mode],
  [
    AC_DEFINE(PROFILE)
    CFLAGS="$CFLAGS -pg"
    CXXFLAGS="$CXXFLAGS -pg"
    LFLAGS="$LFLAGS -pg"
  ])

AC_ARG_ENABLE(memory-debug-mode,
[  --enable-memory-debug-mode     enable memory debug mode],
  [
    AC_DEFINE(DEBUG_MEMORY)
  ])

AC_ARG_ENABLE(func-name-debug-mode,
[  --enable-func-name-debug-mode     enable func name debug mode],
  [
    AC_DEFINE(PRINT_FUNC_DEBUG)
  ])


AC_ARG_ENABLE(stack-trace,
[  --enable-stack-trace     enable stack trace on exceptions],
  [
    AC_DEFINE(ENABLE_STACK_TRACE_ON_EXCEPTIONS)
  ])

AC_ARG_ENABLE(check-null-references,
[  --disable-check-null-references     disable checking for NULL references])

if test "$enable_check_null_references" != no; then
    AC_DEFINE(CHECK_NULL_REFERENCES)
fi

AC_ARG_ENABLE(check-array-indexing,
[  --disable-check-array-indexing     disable checking valid array indexes])

if test "$enable_check_array_indexing" != no; then
    AC_DEFINE(CHECK_ARRAY_INDEXING)
fi

AC_ARG_ENABLE(gnu-pth,
[  --enable-gnu-pth   use pth library],
  [

  AC_DEFINE(USE_GNU_PTH)

  case "$host" in
  *-*-linux*)
    PLATFORMDIR="posix"
	 CXXBINLINKFLAGS="-lpth $CXXBINLINKFLAGS"
  ;;
  esac
  ],[
  case "$host" in
  *-*-linux*)
	 CXXBINLINKFLAGS="-lpthread $CXXBINLINKFLAGS"
	 CFLAGS="$CFLAGS -D_REENTRANT"
	 CXXFLAGS="$CXXFLAGS -D_REENTRANT"
  ;;
  esac 
  ])

AC_ARG_WITH(search-dir,
	[ --with-search-dir=PATH   Specify an additional directory to look for include/ and lib/ sub dirs ],
	[
      if test "x$withval" != "xno" ; then
        	CXXFLAGS="$CXXFLAGS -I$withval/include" 
        	CFLAGS="$CFLAGS -I$withval/include" 
        	LDFLAGS="$LDFLAGS -L$withval/lib" 
      fi
	]
)

AC_ARG_ENABLE(ssl,
[  --disable-ssl     disable ssl integration])

if test "$enable_ssl" != no; then

# The big search for OpenSSL.  Thanks to the openssh configure.in file!
AC_ARG_WITH(ssl-dir,
   [  --with-ssl-dir=PATH     Specify path to OpenSSL installation ],
   [
      if test "x$withval" != "xno" ; then
         tryssldir=$withval
      fi
   ]
)

saved_LIBS="$LIBS"
saved_LDFLAGS="$LDFLAGS"
saved_CPPFLAGS="$CPPFLAGS"
if test "x$prefix" != "xNONE" ; then
   tryssldir="$tryssldir $prefix"
fi
AC_CACHE_CHECK([for OpenSSL directory], ac_cv_openssldir, [
   for ssldir in $tryssldir "" /usr/local/openssl /usr/lib/openssl /usr/local/ssl /usr/lib/ssl /usr/local /usr/pkg /opt /opt/openssl ; do
      CPPFLAGS="$saved_CPPFLAGS"
      LDFLAGS="$saved_LDFLAGS"
      LIBS="$saved_LIBS -lssl -lcrypto"
 
      # Skip directories if they don't exist
      if test ! -z "$ssldir" -a ! -d "$ssldir" ; then
         continue;
      fi
      if test ! -z "$ssldir" -a "x$ssldir" != "x/usr"; then
         # Try to use $ssldir/lib if it exists, otherwise
         # $ssldir
         if test -d "$ssldir/lib" ; then
            LDFLAGS="$saved_LDFLAGS -L$ssldir/lib"
            if test ! -z "$need_dash_r" ; then
               LDFLAGS="$LDFLAGS -R$ssldir/lib"
            fi
         else
            LDFLAGS="$saved_LDFLAGS -L$ssldir"
            if test ! -z "$need_dash_r" ; then
               LDFLAGS="$LDFLAGS -R$ssldir"
            fi
         fi
         # Try to use $ssldir/include if it exists, otherwise
         # $ssldir
         if test -d "$ssldir/include" ; then
            CPPFLAGS="$saved_CPPFLAGS -I$ssldir/include"
         else
            CPPFLAGS="$saved_CPPFLAGS -I$ssldir"
         fi
      fi
 
      # Basic test to check for compatible version and correct linking
      # *does not* test for RSA - that comes later.
      AC_TRY_RUN(
         [
#include <string.h>
#include <openssl/rand.h>
int main(void)
{
   char a[2048];
   memset(a, 0, sizeof(a));
   RAND_add(a, sizeof(a), sizeof(a));
   return(RAND_status() <= 0);
}
         ],
         [
            found_crypto=1
            break;
         ], 
         [
            did_not_find_crypt=0
         ], 
         [
            did_not_find_crypt=0
         ]
      )
 
      if test ! -z "$found_crypto" ; then
         break;
      fi
   done
 
   if test -z "$found_crypto" ; then
      #AC_MSG_ERROR([Could not find working OpenSSL library, please install or check config.log])
		ssldir=""
   fi
   if test -z "$ssldir" && test ! -z "$found_crypto"; then
      ssldir="(system)"
   fi
 
	LIBS="$saved_LIBS"
   ac_cv_openssldir=$ssldir
])
 
if (test ! -z "$ac_cv_openssldir") ;
 then
   AC_DEFINE(HAVE_OPENSSL)
   LIBS="$LIBS -lssl -lcrypto"
 else
   AC_DEFINE(NO_SSL)
fi

if (test ! -z "$ac_cv_openssldir" && test "x$ac_cv_openssldir" != "x(system)") ; then
	dnl Need to recover ssldir - test above runs in subshell
	ssldir=$ac_cv_openssldir
	if test ! -z "$ssldir" -a "x$ssldir" != "x/usr"; then
		# Try to use $ssldir/lib if it exists, otherwise
		# $ssldir
		if test -d "$ssldir/lib" ; then
			LDFLAGS="$saved_LDFLAGS -L$ssldir/lib"
			if test ! -z "$need_dash_r" ; then
				LDFLAGS="$LDFLAGS -R$ssldir/lib"
			fi
		else
			LDFLAGS="$saved_LDFLAGS -L$ssldir"
			if test ! -z "$need_dash_r" ; then
				LDFLAGS="$LDFLAGS -R$ssldir"
			fi
		fi
		# Try to use $ssldir/include if it exists, otherwise
		# $ssldir
		if test -d "$ssldir/include" ; then
			CPPFLAGS="$saved_CPPFLAGS -I$ssldir/include"
		else
			CPPFLAGS="$saved_CPPFLAGS -I$ssldir"
		fi
	fi
	LIBS="$saved_LIBS -lssl -lcrypto"
fi
else
   AC_DEFINE(NO_SSL)
fi
dnl End of the ssl check

dnl Now check for threading stuff
OLD_LIBS=$LIBS
LIBS="-pthread $LIBS"
AC_MSG_CHECKING("pthread_mutexattr_settype")
AC_TRY_RUN(
    [
#include <pthread.h>
#include <assert.h>
int main()
{
    pthread_mutexattr_t attr;
    pthread_mutex_t mutex;
    int res = 0;
    res = pthread_mutexattr_init(&attr);
    assert(res == 0);
    res = pthread_mutexattr_settype(&attr, PTHREAD_MUTEX_RECURSIVE);
    assert(res == 0);
    res = pthread_mutex_init(&mutex, &attr);
    assert(res == 0);
}
    ],
    [
        AC_DEFINE(HAVE_PTHREAD_MUTEXATTR_SETTYPE)
        AC_MSG_RESULT("yes")
    ],
    [ AC_MSG_RESULT("no") ],
    [ AC_MSG_RESULT("no") ]
)

AC_MSG_CHECKING("pthread_spin_lock")
AC_TRY_RUN(
 [
#include <pthread.h>
#include <assert.h>
int main()
{
	pthread_spinlock_t spinlock;
	pthread_spin_init(&spinlock, 0);
	if (pthread_spin_lock(&spinlock) != 0)
		return 1;
	if (pthread_spin_unlock(&spinlock) != 0)
		return 1;
	return 0;
}
 ],
 [
    AC_DEFINE(HAVE_PTHREAD_SPIN_LOCK)
    AC_MSG_RESULT("yes")
 ],
 [ AC_MSG_RESULT("no") ],
 [ AC_MSG_RESULT("no") ]
)
LIBS=$OLD_LIBS
 
#####################################################
# Set the location of the state dir
if test "x$localstatedir" != "x\${prefix}/var"; then
	tempDir=$localstatedir
else
	if test "x$prefix" != "xNONE"; then
		tempDir=${prefix}/var
	else
		tempDir=${ac_default_prefix}/var
	fi
fi
AC_DEFINE_UNQUOTED(DEFAULT_STATE_DIR, "${tempDir}")

#####################################################
# Set the location of the lib dir
if test "x$libdir" != "x\${exec_prefix}/lib"; then
	tempDir=$libdir
else
	if test "x$exec_prefix" != "xNONE"; then
		tempDir=${exec_prefix}/lib
	else
		tempDir=${ac_default_prefix}/lib
	fi
fi
AC_DEFINE_UNQUOTED(DEFAULT_LIB_DIR, "${tempDir}")


#####################################################
# Set the location of the libexec dir
if test "x$libexecdir" != "x\${exec_prefix}/libexec"; then
	tempDir=$libexecdir
else
	if test "x$exec_prefix" != "xNONE"; then
		tempDir=${exec_prefix}/libexec
	else
		tempDir=${ac_default_prefix}/libexec
	fi
fi
AC_DEFINE_UNQUOTED(DEFAULT_LIBEXEC_DIR, "${tempDir}")

#####################################################
# Set the location of the sysconf dir
if test "x$sysconfdir" != "x\${prefix}/etc"; then
  tempDir=$sysconfdir
else
  if test "x$prefix" != "xNONE"; then
    tempDir=${prefix}/etc
  else
    tempDir=${ac_default_prefix}/etc
  fi
fi
AC_DEFINE_UNQUOTED(DEFAULT_SYSCONF_DIR, "${tempDir}")

CXXSHAREDLIBLINKFLAGS="$CXXCOMMONLINKFLAGS $CXXSHAREDLIBLINKFLAGS"
CXXBINLINKFLAGS="$CXXCOMMONLINKFLAGS $CXXBINLINKFLAGS $LDFLAGS"

AC_SUBST(CXXSHAREDLIBLINKFLAGS)
AC_SUBST(CXXBINLINKFLAGS)
AC_SUBST(CXXLINK)
AC_SUBST(CLINK)

AC_OUTPUT(./Makefile
./db/Makefile
./db/btree/Makefile
./db/db/Makefile
./db/include/Makefile
./db/mpool/Makefile
./db/test/Makefile
./doc/Makefile
./doc/man/Makefile
./doc/man/man1/Makefile
./doc/man/man8/Makefile
./etc/Makefile
./etc/init/Makefile
./etc/pam.d/Makefile
./etc/sysconfig/Makefile
./etc/sysconfig/daemons/Makefile
./mof/Makefile
./schemas/Makefile
./schemas/cim25/Makefile
./schemas/cim27/Makefile
./src/Makefile
./src/authenticators/Makefile
./src/authenticators/simple/Makefile
./src/authenticators/pam/Makefile
./src/authenticators/pamcl/Makefile
./src/cim/Makefile
./src/cimom/Makefile
./src/cimom/indication/Makefile
./src/cimxmllistener/Makefile
./src/client/Makefile
./src/client/test/Makefile
./src/common/Makefile
./src/common/socket/Makefile
./src/common/socket/test/Makefile
./src/http/Makefile
./src/http/client/Makefile
./src/http/common/Makefile
./src/http/test/Makefile
./src/http/test/certs/Makefile
./src/ifcs/Makefile
./src/mof/Makefile
./src/provider/Makefile
./src/providerifcs/Makefile
./src/providerifcs/cpp/Makefile
./src/providerifcs/simplecpp/Makefile
./src/providerifcs/cmpi/Makefile
./src/providerifcs/cmpi/common/Makefile
./src/providerifcs/npi/Makefile
./src/providerifcs/npi/common/Makefile
./src/providerifcs/perl/Makefile
./src/providerifcs/perl/perlProvider/Makefile
./src/providers/Makefile
./src/providers/cpp/Makefile
./src/providers/cpp/indicationexport/Makefile
./src/providers/cpp/polled/Makefile
./src/providers/cpp/polled/slp/Makefile
./src/providers/cpp/instance/Makefile
./src/providers/cpp/instance/CIM_IndicationSubscription/Makefile
./src/providers/cpp/instance/CIM_IndicationFilter/Makefile
./src/providers/cpp/instance/OpenWBEM_ObjectManager/Makefile
./src/providers/cpp/instance/CIM_Namespace/Makefile
./src/providers/cpp/instance/CIM_NamespaceInManager/Makefile
./src/providers/cpp/indication/Makefile
./src/providers/cpp/indication/IndicationRepLayer/Makefile
./src/repositories/Makefile
./src/repositories/hdb/Makefile
./src/requesthandlers/Makefile
./src/requesthandlers/binary/Makefile
./src/requesthandlers/cimxml/Makefile
./src/server/Makefile
./src/services/Makefile
./src/services/http/Makefile
./src/tools/Makefile
./src/tools/owmofc/Makefile
./src/wql/Makefile
./src/wql/common/Makefile
./src/xml/Makefile
./test/Makefile
./test/CppUnit/Makefile
./test/acceptance/Makefile
./test/acceptance/progs/Makefile
./test/acceptance/testfiles/Makefile
./test/c++providers/Makefile
./test/c++providers/associator/Makefile
./test/c++providers/method/Makefile
./test/c++providers/property/Makefile
./test/c++providers/instance/Makefile
./test/c++providers/indication/Makefile
./test/npiproviders/Makefile
./test/cmpiproviders/Makefile
./test/unit/Makefile)

AC_CREATE_PREFIX_CONFIG_H([${srcdir}/src/common/OW_config.h], OW, [config.h])
